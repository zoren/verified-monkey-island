Initial { bones.loc := Graves,
bra.loc := LaundryShip,
bucket.cont := Empty,
bucket.loc := LaundryShip,
bucketask := False,
cheesesq.loc := Inn,
doll.loc := VoodooShack,
hasbeenhired := False,
hasbeenrobbed := False,
hatch := Closed,
hatch.sticked := False,
hatch.stringed := False,
isHired := False,
job := Unavailable,
knife.loc := Kitchen,
knowPolish := False,
largoembargo := Enforced,
largohasdirtyshirt := False,
ldoor := Closed,
money := None,
monocle.loc := WallysShip,
paper.hasspit := False,
paper.loc := WallysShip,
pins.loc := VoodooShack,
player.loc := WoodtickRight,
polish.loc := Woodsmith,
rat.inbox := False,
rat.loc := LaundryShip,
rat.trapped := False,
riches.loc := Inv,
shovel.loc := WoodtickRight,
spit.loc := Undefined,
stick.loc := Beach,
string.loc := VoodooShack,
talkedaboutLargo := False,
ticket.loc := Undefined,
toupee.loc := Largosroom,
tried := False,
alligator.loc := Inn,
innkeeper.loc := Inn,
rope.loc := Inn }

{ player.loc = WoodtickRight } ?
    { shovel.loc = WoodtickRight } ?
        pickUp(shovel) >> {shovel.loc := Inv} & { "You pick up a shovel" }
    { hasbeenrobbed != True } ?
        walkto(path) >> {riches.loc := Undefined; hasbeenrobbed := True} & { "Largo comes and take your riches" }
        walkto(bridge) >> {riches.loc := Undefined; hasbeenrobbed := True} & { "Largo comes and take your riches" }
    { hasbeenrobbed = True } ?
        walkto(path) >> {player.loc := Map} & { "You are now looking at the map of Scabb Island" }
        walkto(bridge) >> {player.loc := WoodtickLeft} & { "You are among several ships" }

{ player.loc = WoodtickLeft } ?
    { largohasdirtyshirt = True } ?
        walkto(laundry) >> {ticket.loc := Largosroom; largohasdirtyshirt := False; player.loc := LaundryShip} & { "You are at the laundry. Largo is talking to the laundry guy" }
    walkto(wallyDoor) >> {player.loc := WallysShip} & { "You enter the cartographers ship" }
    walkto(laundry) >> {player.loc := LaundryShip} & { "You are at the laundry" }
    walkto(window) >> {player.loc := Kitchen} & { "You jump through the window very elegantly and fall into the kitchen of the Bloddylips Inn" }
    walkto(hatch) >> {player.loc := BloddyLips} & { "You enter the BloddyLips Inn" }
    walkto(wooddoor) >> {player.loc := Woodsmith} & { "You are in the Woodsmith's place" }
    walkto(inndoor) >> {player.loc := Inn} & { "You enter the Inn. There's rumors Largo has a room here" }
    walkto(bridge) >> {player.loc := WoodtickRight} & { "You are just outside Woodtick" }

{ player.loc = Inn } ?
    walkto(doorway) >> {player.loc := WoodtickLeft} & { "You are among several ships" }
    { knife.loc = Inv, rope.loc = Inn } ?
        use(knife, rope) >> {rope.loc := Undefined; innkeeper.loc := Undefined; alligator.loc := Undefined} & { "You cut the rope holding the Alligator. It runs out closely chased by the innkeeper" }
    { alligator.loc != Inn, cheesesq.loc = Inn } ?
        pickUp(cheesesq) >> {cheesesq.loc := Inv} & { "You pick up delicious cheese squiggles" }
    { ldoor = Closed, innkeeper.loc != Inn } ?
        open(door) >> {ldoor := Open} & { "The door to Largo's room is now open" }
    { ldoor = Open } ?
        walkto(door) >> {player.loc := Largosroom} & { "You enter Largo's room" }
        close(door) >> {ldoor := Closed} & { "The door to Largo's room is now closed" }

{ player.loc = Largosroom } ?
    { toupee.loc = Largosroom } ?
        pickUp(toupee) >> {toupee.loc := Inv} & { "You pick up Largos toupee" }
    { ldoor = Open } ?
        close(door) >> {ldoor := Closed} & { "The door closes -- almost" }
        walkto(door) >> {player.loc := Inn} & { "You are back in the inn" }
    { ldoor = Closed, bucket.cont = Mud } ?
        use(bucket, door) >> {largohasdirtyshirt := True; bucket.cont := Empty} & { "You put the bucket'o'mud on the top of the door. Largo enters and the bucket ends up on his head. Very funny." }
    { ldoor = Closed } ?
        open(door) >> {ldoor := Open} & { "You open the door" }
        { ticket.loc = Largosroom } ?
            pickUp(ticket) >> {ticket.loc := Inv} & { "You got the cleaning ticket for Largo's clothes" }
    { largoembargo = Enforced, doll.loc = Inv, pins.loc = Inv } ?
        use(doll, pins) >> {largoembargo := Alleviated; player.loc := VoodooShack} & { "You quickly stap the doll with the pins. Largo is clearly upset and leaves Scapp Island. You are now back at the voodooschack" }

{ player.loc = LaundryShip } ?
    { hatch.sticked != True, hatch = Open } ?
        walkto(path) >> {player.loc := WoodtickLeft; hatch := Closed} & { "You hear the hatch close as you go back to the several-ships place" }
    { hatch.sticked = True } ?
        walkto(path) >> {player.loc := WoodtickLeft} & { "You are among several ships" }
    walkto(path) >> {player.loc := WoodtickLeft} & { "You are among several ships" }
    { ticket.loc = Inv } ?
        give(ticket, laundryGuy) >> {ticket.loc := Undefined; bra.loc := Inv} & { "You give the ticket to the deaf laundry guy. You now have Largo's bra!" }
    { hatch = Closed } ?
        open(hatch) >> {hatch := Open} & { "You hold the hatch open" }
    { hatch = Open } ?
        { hatch.sticked != True, cheesesq.loc = Inv } ?
            use(cheesesq, hatch) >> {hatch := Closed; cheesesq.loc := LaundryShip} & { "You put the cheese squiggles into the box. The hatch closes." }
        { hatch.sticked != True, stick.loc = Inv } ?
            use(stick, hatch) >> {hatch.sticked := True; stick.loc := LaundryShip} & { "The hatch is now held open by the stick." }
        { hatch.sticked = True, cheesesq.loc = Inv } ?
            use(cheesesq, hatch) >> {cheesesq.loc := LaundryShip; rat.inbox := True} & { "You put the cheese squiggles into the box. The rat enters the box." }
    { bucket.loc = LaundryShip } ?
        { tried != True } ?
            trypickUp(bucket) >> {tried := True} & { "You try to pick up the bucket, but the men of low moral fiber tell you not to pick up the bucket" }
        { tried = True } ?
            talkto(molmf, yours) >> {bucket.loc := Inv} & { "You ask if the bucket is theirs. It's not, so you get it" }
            { bucketask != True } ?
                talkto(molmf, please) >> {bucketask := True} & { "You kindly ask to have the bucket, but they persist not to let you have the bucket" }
            { bucketask = True } ?
                talkto(molmf, please) >> {bucket.loc := Inv} & { "Finally the men of low moral fiber give up and let you have the bucket" }
    { hatch.sticked = True, string.loc = Inv } ?
        use(string, stick) >> {hatch.stringed := True; string.loc := LaundryShip} & { "The string is now tied to the stick. The trap is ready!" }
    { rat.inbox = True } ?
        { hatch.stringed = True } ?
            pull(string) >> {string.loc := Inv; stick.loc := Inv; rat.trapped := True; hatch.sticked := False; hatch.stringed := False; hatch := Closed} & { "Bum! The hatch closes and the rat is trapped." }
    { hatch = Open, rat.trapped = True } ?
        pickUp(rat) >> {rat.loc := Inv; rat.trapped := False; hatch := Closed} & { "You pick up the rat from the box" }

{ player.loc = WallysShip } ?
    { paper.loc = WallysShip } ?
        pickUp(paper) >> {paper.loc := Inv} & { "You pick up an piece of paper" }
    { monocle.loc = WallysShip } ?
        pickUp(monocle) >> {monocle.loc := Inv} & { "You steal Wally's monocle" }
    { monocle.loc = Inv } ?
        give(monocle, wally) >> {monocle.loc := WallysShip} & { "You give back the monocle to Wally. Poor guy." }
    walkto(door) >> {player.loc := WoodtickLeft} & { "You are among several ships" }

{ player.loc = BloddyLips } ?
    { talkedaboutLargo != True } ?
        talkto(bartender, largo) >> {spit.loc := BloddyLips; talkedaboutLargo := True} & { "As you talk about Largo, he enters, takes the bartenders money and spits on the wall." }
    { paper.loc = Inv, spit.loc = BloddyLips } ?
        use(paper, spit) >> {paper.hasspit := True; spit.loc := Undefined} & { "You scrape the spit off of the wall using the paper. You now have spit-encrusted paper." }
    { rat.loc = Vichyssoise, hasbeenhired != True, job != Available } ?
        talkto(bartender, howisstew) >> {job := Available} & { "You ask for some stew knowingly that you just ruined it with the rat. The current chef is fired and the job is now available. Maybe something for you?" }
    { job = Available, hasbeenhired != True } ?
        talkto(bartender, askforjob) >> {hasbeenhired := True; job := Unavailable; money := Money420; player.loc := Kitchen} & { "You are now hired. The pay is paid in advance and you enter the kitchen with no intention whatsoever of fullfilling your newfound duties." }
    walkto(doorway) >> {player.loc := WoodtickLeft} & { "You leave the BloddyLips and are now among several ships" }

{ player.loc = Kitchen } ?
    { knife.loc = Kitchen } ?
        pickUp(knife) >> {knife.loc := Inv} & { "You take the chef's knife" }
    { rat.loc = Inv } ?
        use(rat, stew) >> {rat.loc := Vichyssoise} & { "You put the rat in the Vichyssoise" }
    walkto(window) >> {player.loc := WoodtickLeft} & { "You crawl out the window. You are among several ships" }
    walkto(door) >> {player.loc := BloddyLips} & { "You enter the BloddyLips from the kitchen" }

{ player.loc = Woodsmith } ?
    walkto(path) >> {player.loc := WoodtickLeft} & { "You are among several ships" }

{ player.loc = VoodooShack } ?
    walkto(coffinpath) >> {player.loc := Coffin} & { "You enter the coffin" }
    { string.loc = VoodooShack } ?
        pickUp(string) >> {string.loc := Inv} & { "You pick up a piece of string" }
    { shoppinglist.loc != Inv } ?
        talkto(lady, aboutlargoCurse) >> {shoppinglist.loc := Inv} & { "You talk to the voodoolady about Largo. You get a shoppinglist with four items to collect" }
    { shoppinglist.loc = Inv } ?
        { bones.loc = Inv } ?
            give(bones, lady) >> {bones.loc := VoodooShack} & { "You give the bones to the lady" }
        { bra.loc = Inv } ?
            give(bra, lady) >> {bra.loc := VoodooShack} & { "You give the bra to the lady" }
        { toupee.loc = Inv } ?
            give(toupee, lady) >> {toupee.loc := VoodooShack} & { "You give the toupee to the lady" }
        { paper.hasspit = True } ?
            give(paper, lady) >> {paper.hasspit := False; spit.loc := VoodooShack} & { "You give the spit-encrusted paper to the lady. You get to keep the paper without the spit" }
    { bones.loc = VoodooShack, bra.loc = VoodooShack, toupee.loc = VoodooShack, spit.loc = VoodooShack } ?
        talkto(lady, doll) >> {doll.loc := Inv; pins.loc := Inv; bones.loc := Undefined; bra.loc := Undefined; toupee.loc := Undefined; spit.loc := Undefined} & { "You now have the voodoodoll. Now just find Largo" }
    { largoembargo = Alleviated, bigWhoopBook.loc != Inv } ?
        talkto(lady, escapeLargo) >> {bigWhoopBook.loc := Inv} & { "The lady tells you that you can escape Largo by finding Big Whoop. You get a book about it." }

{ player.loc = Swamp } ?
    { bucket.loc = Inv } ?
        use(bucket, swamp) >> {bucket.cont := Mud; swamp.loc := Undefined} & { "You fill up the bucket with mud." }
    use(coffin) >> {player.loc := Coffin} & { "You enter the coffin. You can now row around in the swamp." }
    walkto(path) >> {player.loc := Map} & { "You are looking at the map of Scabb island" }

{ player.loc = Coffin } ?
    rowto(shore) >> {player.loc := Swamp} & { "You row to the shore" }
    rowto(shackpath) >> {player.loc := VoodooShack} & { "You row into the swamp and find the entrance to the voodooschack" }

{ player.loc = Cemetery } ?
    walkto(path) >> {player.loc := Map} & { "You are looking at the map of Scabb island" }
    walkto(graves) >> {player.loc := Graves} & { "You are at the top of a hill of graves" }

{ player.loc = Graves } ?
    { shovel.loc = Inv } ?
        use(shovel, largosgrave) >> {bones.loc := Inv} & { "You dig up bones from Largo's grave." }
    walkto(cemetery) >> {player.loc := Cemetery} & { "You are at the cemetery" }

{ player.loc = Peninsula } ?
    walkto(path) >> {player.loc := Map} & { "You are looking at the map of Scabb island" }
    walkto(houseboat) >> {player.loc := Dreadsship} & { "You enter Dread's houseboat" }

{ player.loc = Dreadsship } ?
    { monocle.loc = Inv } ?
        give(monocle, dread) >> {monocle.loc := Dread} & { "You give the monocle to Dread. He is happy to have found a new lucky charm" }
    { money != None, largoembargo = Alleviated, monocle.loc = Dread } ?
        talkto(dread, charterShip) >> {money := Money400} & { "You charter Dread's ship" }
    walkto(path) >> {player.loc := Peninsula} & { "You are now just outside Dread's houseboat" }

{ player.loc = Beach } ?
    { stick.loc = Beach } ?
        pickUp(stick) >> {stick.loc := Inv} & { "You pick up a stick" }
    walkto(path) >> {player.loc := Map} & { "You are looking at the map of Scabb island" }

{ player.loc = Map } ?
    walkto(beach) >> {player.loc := Beach} & { "You are at the beach. Nice" }
    walkto(woodtick) >> {player.loc := WoodtickRight} & { "You are just outside Woodtick" }
    walkto(cemetery) >> {player.loc := Cemetery} & { "You enter the creepy cemetery" }
    walkto(peninsula) >> {player.loc := Peninsula} & { "You are at the peninsula just outside Dread's houseboat" }
    walkto(swamp) >> {player.loc := Swamp} & { "You are at the swamp" }